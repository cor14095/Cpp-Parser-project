Vocabulario de Cocol

ident = letter {letter | digit}.

number = digit {digit}.

string = '"' {anyButQuote} '"'.

char = '\'' anyButApostrophe '\''.

Sintaxis de Cocol

Cocol = "COMPILER" ident

ScannerSpecification

ParserSpecification

"END" ident '.'.

ScannerSpecification =

["CHARACTERS" {SetDecl}]

["KEYWORDS" {KeyworDecl}]

["TOKENS" {TokenDecl}]

{WhiteSpaceDecl}.

SetDecl = ident '=' Set.

Set = BasicSet { ('+'|'-') BasicSet }.

BasicSet = string | ident | Char [".." Char].

Char = char | "CHR" '(' number ')'.

KeywordDecl = ident '=' string '.'

TokenDecl = ident ['=' TokenExpr ] ["EXCEPT KEYWORDS"] '.'.

TokenExpr = TokenTerm {'|' TokenTerm }.

TokenTerm = TokenFactor {TokenFactor}

TokenFactor = Symbol

| '(' TokenExpr ')'

| '[' TokenExpr ']'

| '{' TokenExpr '}'.

Symbol = ident | string | char

WhiteSpaceDecl = "IGNORE" Set

ParserSpecification = "PRODUCTIONS" {Production}.

Production = ident [Attributes] [SemAction] '=' Expression '.'.

Expression = Term { '|' Term }.

Term = Factor {Factor}

Factor = Symbol [Attributes]

| '(' Expression ')'

| '[' Expression ']'

| '{' Expression '}'

| SemAction.

Attributes = "<." {ANY} ".>"

SemAction = "(." {ANY} ".)"